PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

select ?label  (count(?e) as ?events) (count(?upcoming) as ?upcoming_events) (group_concat( distinct ?by ;separator = ', ') as ?attribution)  ?g  

where {
    graph ?g {
        ?e a schema:Event ;
           schema:startDate ?date .
    }
    optional {
        ?e schema:startDate ?upcoming .
        filter(?upcoming >= now() || STRDT(str(?upcoming), xsd:dateTime) >= now() )
    }

    OPTIONAL {
        ?g rdfs:label ?datafeed 
    }
    OPTIONAL {
        ?g prov:wasAttributedTo/rdfs:label ?by  
    }
    bind(coalesce(?datafeed, CONCAT("JSON-LD from ",str(?g))) as ?label)
    
} group by ?g ?label  order by DESC(?upcoming_events)